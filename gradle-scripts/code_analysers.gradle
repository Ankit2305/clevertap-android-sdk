// note : we have a STEP0 also. check root/build.gradle
apply plugin: "io.gitlab.arturbosch.detekt"               // detekt STEP1 applying plugin (detekt = kotlin code's static analyser)
apply plugin: "checkstyle"                                // checkstyle STEP1 applying plugin (checkstyle = java code's static analyser)

// detekt STEP2 plugin configuration
detekt {
    parallel = true                                                                    // Builds the AST in parallel. Rules are always executed in parallel.Can lead to speedups in larger projects. `false` by default.
    allRules = true                                                                    // Turns on all the rules. `false` by default.
    ignoreFailures = true                                                              // If set to `true` the build does not fail when the maxIssues count was reached. Defaults to `false`.
    debug = true                                                                       // Adds debug output during task execution. `false` by default.
}

// detekt STEP3 gradle task configuration
tasks.named("detekt").configure {
    reports {
        html.required.set(true)// Enable/Disable HTML report (default: true)
        html.outputLocation.set(file("$buildDir/reports/detekt/detekt.html"))//file(rootProject.buildDir.toString() + "/reports/detekt-${project.name}.html")) //for base module
        sarif.required.set(false)
        xml.required.set(false)
        txt.required.set(false)
    }
}


// checkstyle STEP2 plugin configuration
checkstyle {
    //configFile rootProject.file('checkstyle.xml') // will automatically take from /config/checkstyle/checkstyle.xml
    ignoreFailures false
    showViolations true
    toolVersion = "7.8.1"
}

// checkstyle STEP3 gradle task configuration
task Checkstyle(type: Checkstyle) {
    //configFile rootProject.file('checkstyle.xml') // will automatically take from /config/checkstyle/checkstyle.xml
    source 'src/main/java'
    ignoreFailures true
    showViolations true
    include '**/*.java'
    classpath = files()
}

// checkstyle STEP4 (optional) add the checkstyle task manually to  `gradle check` command. for detekt, it automatically adds itself
tasks.named("check").configure {
    dependsOn "Checkstyle"
}
