apply plugin: 'com.android.library'
apply plugin: 'com.huawei.agconnect'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'
apply plugin: 'kotlin-android'

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ])
}

ext {
    bintrayRepo = 'Maven'
    bintrayName = 'HuaweiPushSDK'
    org = 'clevertap'

    publishedGroupId = 'com.clevertap.android'
    libraryName = 'HuaweiPushSDK'
    artifact = 'clevertap-huawei-sdk'
    libraryDescription = 'The CleverTap Android Huawei Push SDK'

    siteUrl = 'https://github.com/CleverTap/clevertap-android-sdk/clevertap-hms'
    gitUrl = 'https://github.com/CleverTap/clevertap-android-sdk.git'

    libraryVersion = '1.0.0'

    developerId = 'clevertap'
    developerName = 'CleverTap'
    developerEmail = 'support@clevertap.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 10000
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            testCoverageEnabled true //To get coverage reports for instrumentation tests
            buildConfigField "String", "SDK_VERSION_STRING",
                    '"!SDK-VERSION-STRING!:com.clevertap.android:clevertap-huawei-sdk:1.0.0.0"'
        }
        release {
            buildConfigField "String", "SDK_VERSION_STRING",
                    '"!SDK-VERSION-STRING!:com.clevertap.android:clevertap-huawei-sdk:1.0.0.0"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            if (variant.getName().toLowerCase().contains('release')) {
                outputFileName = "${artifact}-${libraryVersion}.aar"
            } else if (variant.getName().toLowerCase().contains('debug')) {
                outputFileName = "${artifact}-${variant.name}-${libraryVersion}.aar"
            }
        }
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jvmArgs '-noverify'
        }
    }
}

version = libraryVersion
group = publishedGroupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    dryRun = false
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = org
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}

ext {
    bintrayRepo = 'Maven'
    bintrayName = 'HuaweiSDK'
    org = 'clevertap'

    publishedGroupId = 'com.clevertap.android'
    libraryName = 'HuaweiSDK'
    artifact = 'clevertap-huawei-sdk'
    libraryDescription = 'The CleverTap Android Huawei Push SDK'

    libraryVersion = '1.0.0'

    developerId = 'clevertap'
    developerName = 'CleverTap'
    developerEmail = 'support@clevertap.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

dependencies {
    compileOnly project(':clevertap-core')
    implementation 'com.huawei.hms:push:5.0.0.300'
    compileOnly('androidx.annotation:annotation:1.1.0')

    testImplementation project(':test_shared')
    testImplementation 'com.huawei.hms:push:5.0.0.300'
    testImplementation "androidx.core:core-ktx:1.3.1"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}